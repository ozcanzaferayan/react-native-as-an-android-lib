apply plugin: "com.android.library"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    implementation 'com.walmartlabs.ern:react-native:0.62.2'
    implementation 'com.zaferayan.integration:android-jsc-r245459:1.0.0'
    implementation 'com.zaferayan.integration:react-native-video:1.0.0'

}

apply plugin: 'maven-publish'

def libVersion = '1.0.1'
def libGroupId = 'com.zaferayan.integration'
def libArtifactId = 'sample-rn-app-flow-library'

def repoUrl = 'https://maven.pkg.github.com/ozcanzaferayan/react-native-as-an-android-lib'
def repoName = "GitHubPackages"
def repoUsername = 'ozcanzaferayan'
def repoPassword = 'GITHUB_ACCESS_TOKEN' // Buraya Github üzerinden aldığınız değeri yapıştırın

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                groupId libGroupId
                artifactId libArtifactId
                version libVersion
                artifact bundleReleaseAar

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }

        repositories {
            maven {
                name = repoName
                url = uri(repoUrl)
                credentials {
                    username = repoUsername
                    password = repoPassword
                }
            }
        }
    }
}